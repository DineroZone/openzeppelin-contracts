// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/*
 * @dev Context variant with ERC2771 support.
 */
abstract contract BaseRelayRecipient is Context {
    address immutable _trustedForwarder;

    constructor(address trustedForwarder) {
        _trustedForwarder = trustedForwarder;
    }

    function isTrustedForwarder(address forwarder) public view virtual returns(bool) {
        return forwarder == _trustedForwarder;
    }

    function _msgSender() internal view virtual override returns (address sender) {
        if (isTrustedForwarder(msg.sender)) {
            // return abi.decode(abi.encodePacked(bytes12(0), msg.data[msg.data.length-20:]), (address));
            assembly { sender := shr(96, calldataload(sub(calldatasize(), 20))) }
        } else {
            return Context._msgSender();
        }
    }

    function _msgData() internal view virtual override returns (bytes calldata) {
        if (isTrustedForwarder(msg.sender)) {
            return msg.data[:msg.data.length-20];
        } else {
            return Context._msgData();
        }
    }
}
